Запрос = Новый Запрос;
Запрос.Текст =
    "ВЫБРАТЬ
	|	ЕдиницыИзмерения.Ссылка,
    |	ЕдиницыИзмерения.Наименование,
	|	ЕдиницыИзмерения.Владелец,
	|	ЕдиницыИзмерения.Владелец.Наименование КАК ВладелецНаименование,
	|	ВЫРАЗИТЬ(ЕдиницыИзмерения.Владелец КАК Справочник.ХарактеристикиНоменклатуры).Владелец КАК НоменклатураХарактеристики,
	|	ЕдиницыИзмерения.ЕдиницаПоКлассификатору.Ссылка КАК ЕдиницаПоКлассификатору,
	|	ЕдиницыИзмерения.ЕдиницаПоКлассификатору.Наименование КАК ЕдиницаПоКлассификаторуНаименование
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|ГДЕ
	|	ЕдиницыИзмерения.Ссылка = &Ссылка
    |	И НЕ ТИПЗНАЧЕНИЯ(ЕдиницыИзмерения.Владелец) = ТИП(Справочник.НоменклатурныеГруппы)
	|";
Запрос.Параметры.Вставить("Ссылка", ОбъектОбработки.Ссылка);
	
РезультатЗапроса = Запрос.Выполнить();

Если РезультатЗапроса.Пустой() Тогда
	СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;
	ТекстОшибки = "Некорректный владелец единицы измерения";
	ВызватьИсключение ТекстОшибки;
КонецЕсли; 

НоменклатураДляПроверки = Справочники.Номенклатура.ПустаяСсылка();

ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать(); 
	
Если ВыборкаДетальныеЗаписи.Следующий() Тогда
	      
    СтруктураДанныхВладелец = Новый Структура;
	СтруктураДанныхВладелец.Вставить("Ссылка",          XMLСтрока(ВыборкаДетальныеЗаписи.Владелец));
	СтруктураДанныхВладелец.Вставить("Наименование",    ВыборкаДетальныеЗаписи.ВладелецНаименование);			             
		
	Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.НоменклатураХарактеристики) Тогда
		НоменклатураДляПроверки = ВыборкаДетальныеЗаписи.Владелец;
	Иначе
		НоменклатураДляПроверки = ВыборкаДетальныеЗаписи.НоменклатураХарактеристики;
	КонецЕсли;

    СтруктураДанныхПоКлассификатору = Новый Структура;
	СтруктураДанныхПоКлассификатору.Вставить("Ссылка",          XMLСтрока(ВыборкаДетальныеЗаписи.ЕдиницаПоКлассификатору));
	СтруктураДанныхПоКлассификатору.Вставить("Наименование",    ВыборкаДетальныеЗаписи.ЕдиницаПоКлассификаторуНаименование);
КонецЕсли;

Если Не ЗначениеЗаполнено(НоменклатураДляПроверки) Тогда
    СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;
	ТекстОшибки = "Некорректный владелец единицы измерения";
	ВызватьИсключение ТекстОшибки;
КонецЕсли;

//REQ1C-5188
Если ТипЗнч(НоменклатураДляПроверки) = Тип("СправочникСсылка.Номенклатура") Тогда

    свВыгруженВWMSAxelot = ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоНаименованию("Выгружен в WMS Axelot", Истина);
    ВыгруженВАкселот = ДополнительныеФункции.ПрочитатьСвойствоОбъекта(НоменклатураДляПроверки, свВыгруженВWMSAxelot, Истина);

    Если Не ВыгруженВАкселот Тогда
        СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;
	    ТекстОшибки = СтрШаблон("Владелец единицы измерения (%1) не выгружен в Акселот", НоменклатураДляПроверки.Код);
	    ВызватьИсключение ТекстОшибки;
    КонецЕсли;

КонецЕсли;
//REQ1C-5188

Результат = ВыполнитьФункцию("ПроверитьНоменклатуруНаВыгрузку", НоменклатураДляПроверки);

Если Не Результат Тогда
    СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;
	ТекстОшибки = "Единица измерения не подлежит выгрузке";
	ВызватьИсключение ТекстОшибки;
КонецЕсли;

СтруктураДанныхРодитель = Новый Структура;
СтруктураДанныхРодитель.Вставить("Ссылка",          "00000000-0000-0000-0000-000000000000");
СтруктураДанныхРодитель.Вставить("Наименование",    "");

Данные = Новый Структура;

Данные.Вставить("Ссылка",               XMLСтрока(ОбъектОбработки.Ссылка));
Данные.Вставить("ПометкаУдаления",      ОбъектОбработки.ПометкаУдаления);
Данные.Вставить("Владелец",             СтруктураДанныхВладелец);
Данные.Вставить("Родитель",             СтруктураДанныхРодитель);
Данные.Вставить("Наименование",         ОбъектОбработки.Наименование);
Данные.Вставить("Высота",               ОбъектОбработки.Высота);
Данные.Вставить("Глубина",              ОбъектОбработки.Глубина);
Данные.Вставить("ЕдиницаИзмерения",     СтруктураДанныхПоКлассификатору);
Данные.Вставить("Масса",                "");
Данные.Вставить("МассаНетто",           "");
Данные.Вставить("НаименованиеПолное",   "");
Данные.Вставить("Объем",                ОбъектОбработки.Объем/1000);
Данные.Вставить("Ширина",               ОбъектОбработки.Ширина);
Данные.Вставить("ДатаРегистрации",      XMLСТрока(ДатаРегистрации));
Данные.Вставить("Коэффициент",          ОбъектОбработки.Коэффициент);

РезультатОбработки.Body = сшпОбщегоНазначения.ПреобразоватьСтруктуруПоФормату(ФорматСообщения, Данные);
